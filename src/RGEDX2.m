RGEDX2 ;MSC/IND/DKM - Programmer extensions ;04-May-2006 08:19;DKM
 ;;3.0;EXTENSIBLE EDITOR;;Jan 23, 2015;Build 12
 ;;
 ;=================================================================
 ; Display table of ASCII codes
ASCII N RGZ,RGZ1,RGZ2,RGZ3
 F RGZ=1:1:127 D  G:RGZ3=U A1
 .I ($Y'<RGY2)!(RGZ=1) D  Q:RGZ3=U
 ..S RGZ3=$S(RGZ>1:$$PRMPT^RGED2(34,1),1:""),$Y=0
 ..W $$XY^RGUT(0,RGY1),RGEOS,*13
 .F RGZ1=0:1:4 D
 ..S RGZ2=RGZ1*(RGY2-RGY1)+RGZ
 ..Q:RGZ2>127
 ..W ?(RGZ1*15+5-$L(RGZ2)),RGZ2,?(RGZ1*15+6),"= ",$S(RGZ2<32:U_$C(RGZ2+64),RGZ2=127:"DEL",1:$C(RGZ2))
 .W !
 W $$PRMPT^RGED2(35,1)
A1 D PAINT^RGEDS
 Q
 ; Calculator
CALC N RGVAL,RGZ
 S @$$TRAP^RGUTOS("C1^RGEDX2"),RGZ=$$PRMPT^RGED2(36,999,"","",$G(^XTMP(RGPID,"RGEDCALC")))
 Q:U[RGZ
 I $G(DUZ(0))'="@",RGZ["@"!(RGZ[U) W $$PRMPT^RGED2(-67,0) Q
 S ^XTMP(RGPID,"RGEDCALC")=RGZ,@("RGVAL="_RGZ)
 D DL1^RGED4(RGVAL,1)
 W $$PRMPT^RGED2(37,0)
 Q
C1 D ERR^RGUTOS("","",.RGZ)
 W $$PRMPT^RGED2($S(RGZ="":-38,1:RGZ),0)
 Q
 ; Justify comments and line labels
CMNT N RGZ,RGZ1,RGZ2,RGZ3,RGZ4,RGZ5
 D UPDATE^RGED2
 W $$PRMPT^RGED2(39,0)
 F RGZ=1:1:RGLC D
 .S RGZ1=$G(^XTMP(RGPID,RGBUF,RGZ)),RGZ2=0,RGZ3=""
 .Q:RGZ1=""
 .S:$E(RGZ1)'=" " RGZ3=$P(RGZ1," "),RGZ1=$P(RGZ1," ",2,999)
 .S RGZ1=$$TRIM^RGUT(RGZ1)
 .I '$L(RGZ1),$E(RGZ3)'="*" S RGZ1=";"
 .S RGZ1=RGZ3_$E("        ",$L(RGZ3)+1,8)_" "_RGZ1,RGCHG=1
 .S ^XTMP(RGPID,RGBUF,RGZ)=RGZ1
 .Q:RGZ1'[";"
 .F RGZ3=1:1:$L(RGZ1) D
 ..S RGZ4=$E(RGZ1,RGZ3)
 ..I RGZ4="""" S RGZ2=$S(RGZ2=-1:RGZ3,1:-1) Q
 ..Q:RGZ2=-1
 ..S:$C(9)_" ;"'[RGZ4 RGZ2=RGZ3
 ..I RGZ4=";" D  S RGZ3=999 S:($L(RGZ4)+$L(RGZ5)+$L(RGZ1))'>RGMAX ^XTMP(RGPID,RGBUF,RGZ)=RGZ4_RGZ5_RGZ1
 ...I $E(RGZ1,RGZ2)="." S RGZ4=$E(RGZ1,1,RGZ2),RGZ5="",RGZ1=$E(RGZ1,RGZ3,RGMAX)
 ...E  I RGZ2,RGZ2'<$F(RGZ1," ") S RGZ4=$E(RGZ1,1,RGZ2),RGZ5=$$REPEAT^XLFSTR(" ",$S(RGZ2'<RGST(8):1,1:RGST(8)-RGZ2-1)),RGZ1=$E(RGZ1,RGZ3,RGMAX)
 ...E  S RGZ4=$S(RGZ2:$E(RGZ1,1,RGZ2),1:$P(RGZ1," ")),RGZ5=$$REPEAT^XLFSTR(" ",$S((RGST(7)-$L(RGZ4))>1:RGST(7)-$L(RGZ4)-1,1:1)),RGZ1=$E(RGZ1,RGZ3,RGMAX)
 S RGLN=^XTMP(RGPID,RGBUF,RGROW)
 D SHOW^RGEDS
 W $$PRMPT^RGED2(40,0)
 Q
 ; Routine size
SIZE N RGZ,RGZ1,RGZ2,RGCNT,RGRTN
 D UPDATE^RGED2
 F RGZ=$S(RGROW>RGLC:RGLC-1,1:RGROW):-1:0 Q:'RGZ  Q:$E($G(^XTMP(RGPID,RGBUF,RGZ)))="*"
 S RGCNT=0,RGRTN=$E($G(^XTMP(RGPID,RGBUF,RGZ)),2,9)
 F RGZ=RGZ+1:1:RGLC S RGZ1=$G(^XTMP(RGPID,RGBUF,RGZ)) Q:$E(RGZ1)="*"  D
 .S RGCNT=RGCNT+$L($P(RGZ1," "))+3,RGZ1=$P(RGZ1," ",2,999)
 .F RGZ2=0:0 Q:$A(RGZ1)'=32  S RGZ1=$E(RGZ1,2,999)
 .S RGCNT=RGCNT+$L(RGZ1)
 W $$PRMPT^RGED2("Routine |RGRTN| contains |RGCNT| bytes.",0)
 Q
 ; Syntax check
SYNTAX(RGL1,RGL2) ;
 N RGCNT,RGZ,RGL,RGRTN
 S:'$D(RGL1) RGL1=1,RGL2=RGLC
 S:'$D(RGL2) RGL2=RGL1
 S:RGL1<1 RGL1=1
 S:RGL1>RGLC RGL1=RGLC
 S:RGL2<1 RGL2=1
 S:RGL2>RGLC RGL2=RGLC
 D UPDATE^RGED2
 S RGCNT=0,RGZ=$$ENTRY^RGUTSTX("","D"),RGRTN="Checking syntax..."
 W $$PRMPT^RGED2(RGRTN,0)
 F RGL=RGL1:1:RGL2 D
 .S RGZ=$G(^XTMP(RGPID,RGBUF,RGL))
 .I $E(RGZ)="*" D  Q
 ..S RGRTN="Checking syntax on "_$E(RGZ,2,999)_"..."
 ..S RGZ=$$PRMPT^RGED2(RGRTN,0)
 ..R *RGZ:0
 ..S:$C(RGZ)=U RGL=RGL2
 .S RGZ=$$ENTRY^RGUTSTX(RGZ,"LDIZ.")
 .I RGZ D
 ..D MOVETO^RGED2(RGL,+$P(RGZ,U,2))
 ..S RGCNT=RGCNT+1,RGZ=$$PRMPT^RGED2($P(RGZ,U,3)_".  Continue checking? "_RGBEL,1,1)
 ..I "Nn^"[RGZ S RGL=RGL2+1
 ..E  W $$PRMPT^RGED2(RGRTN,0)
 W $$PRMPT^RGED2($S(RGCNT=1:"One syntax error",RGCNT>1:RGCNT_" syntax errors",1:"No syntax errors")_" found",0)
 S RGZ=$$ENTRY^RGUTSTX("","I")
 Q
