RGED0 ;MSC/IND/DKM - Continuation of RGED;20-Jan-2015 13:23;DKM
 ;;3.0;EXTENSIBLE EDITOR;;Jan 23, 2015;Build 12
 ;;
 ;=================================================================
 ; Enter command mode
CMD(RGTBL,RGP) ;
 S (RGCH,RGCH2)=$$PRMPT^RGED2($G(RGP,1),0),RGFIN=-1
 D L1
 K:RGFIN=-1 RGFIN
 Q
 ; Enable/disable margin bell
BELL S RGBEL=$S(RGST(9):"",1:$C(7))
 Q
 ; Load text into buffer
LOAD K ^XTMP(RGPID)
 S ^XTMP(RGPID,0)=$$DTC^RGUT(DT,2)_U_DT
 I RGDIC'="" D
 .N RGZ,RGZ1
 .S RGZ=0
 .F RGLC=0:1 S RGZ=$O(@RGDIC@(RGZ)) Q:'RGZ  S RGZ1=^(RGZ,0),^XTMP(RGPID,RGBUF,RGLC+1)=$S(RGST(13)=1:$$FMTHLP(RGZ1),1:RGZ1)
 S RGLN=$G(^XTMP(RGPID,RGBUF,RGROW))
 Q
 ; Format help text
FMTHLP(RGED) ;
 S:RGED["|" RGED=$$MSG^RGUT(RGED,"|")
 S RGED=$TR(RGED,"[]",$C(127,128))
 S RGED=$$SUBST^RGUT(RGED,$C(127,127),"[")
 S RGED=$$SUBST^RGUT(RGED,$C(128,128),"]")
 Q RGED
SAVE ; Save changes
 D:RGBUF'="RGED" SWITCH^RGED5("")
 W $$PRMPT^RGED2(2,0)
 D UPDATE^RGED2
 I RGDIC'="" D
 .N RGZ,RGZ1
 .K @RGDIC
 .F RGZ=1:1:RGLC  S @RGDIC@(RGZ,0)=^XTMP(RGPID,"RGED",RGZ)
 .S @RGDIC@(0)=U_U_RGLC_U_RGLC_U_$$NOW^XLFDT,RGCHG=0
 W $$PRMPT^RGED2(62,0)
 Q
 ; Execute postinitialization (configuration then user level)
START I $$NEWERR^%ZTER N $ET S $ET=""
 S @$$TRAP^RGUTOS("ERROR^RGED0")
 X:$D(^RGEDCFG(RGCF,4)) ^(4)
 X:$D(^RGED("M",RGCF,DUZ,"@")) ^("@")
 W:RGCF2 $$PRMPT^RGED2(-51,0)
 ; Main loop
LOOP Q:$D(RGFIN)
 D SPSN^RGEDS:RGST(6)=1,HLPMSG^RGEDS:'RGMSG&RGST(5)
 W $$XY^RGUT(RGED,RGY)
 S (RGCH,RGCH2)="",RGITER=$S(RGITER2:RGITER2,1:0),RGITER2=0
L1 S RGCH1=$$READCH
 G:RGCH1="" SAVE:$P(^RGEDCFG(RGCF,0),U,7),EXIT
 S RGCH=RGCH_RGCH1,RGZ=$A(RGCH1),RGZ=$S(RGZ>122:RGZ,RGZ<97:RGZ,1:RGZ-32)
 S RGCH2=RGCH2_$C(RGZ\16+65)_$C(RGZ#16+65)
L2 D:RGMSG!RGST(5) HLPMSG^RGEDS
 I $D(^RGED(RGCF,"K",RGTBL,RGCH2)) X (^(RGCH2)) G LOOP
 G:$E($O(^(RGCH2)),1,$L(RGCH2))=RGCH2 L1
 S RGCH1=$E(RGCH),RGCH=$E(RGCH,2,RGMAX),RGCH2=$E(RGCH2,3,RGMAX)
 X:$D(^RGED(RGCF,"K",RGTBL))#10 ^(RGTBL)
 G LOOP:$D(RGFIN),LOOP:RGCH="",L2
 ; Insert a string at current position
INSERT(RGCH,RGITER) ;
INS1 S RGPAD=RGCOL-$L(RGLN)-1
 S:RGPAD<0 RGPAD=0
 I ($L(RGLN)+$L(RGCH)+RGPAD)>RGMAX S RGZ=$$PRMPT^RGED2(-3,0) Q
 I RGROW>RGLC D
 .S RGLC=RGLC+1
 .D SLINE^RGEDS(RGROW)
 .I RGY<RGY2 W ! D SLINE^RGEDS(RGLC+1)
 .W $$XY^RGUT(RGED,RGY)
 S RGLN=$E(RGLN,1,RGCOL-1)_$$REPEAT^XLFSTR(" ",RGPAD)_RGCH_$E(RGLN,RGST(2)*$L(RGCH)+RGCOL,RGMAX),RGCHG=1
 I RGED'>RGED2 D
 .I 'RGST(15) W $S(RGST(2):"",1:RGESC_"["_$L(RGCH)_"@")_$TR(RGCH,RGCTL1,RGCTL2)
 .E  W $TR($E(RGLN,RGCOL,RGCOL+RGED2-RGED),RGCTL1,RGCTL2)
 D MOVETO^RGED2(RGROW,RGCOL+$L(RGCH))
 I RGST(10),RGST(8),$L(RGLN)>RGST(8) D WRAP^RGED1
 I $G(RGITER)>1 S RGITER=RGITER-1 G INS1
 Q
 ; Delete a character
DELETE(RGITER) ;
DEL I RGCOL'>$L(RGLN) D
 .S RGLN=$E(RGLN,1,RGCOL-1)_$E(RGLN,RGCOL+1,RGMAX),RGCHG=1
 .W RGESC_"[P",$$XY^RGUT(RGED2,RGY),$TR($E(RGLN,RGLFT+RGED2),RGCTL1,RGCTL2)
 E  D END^RGED2,JOIN^RGED1
 I $G(RGITER)>1 S RGITER=RGITER-1 G DEL
 Q
 ; Rubout a character
RUBOUT(RGITER) ;
RBT I RGROW=1,RGCOL=1 Q
 I RGCOL-1>$L(RGLN) D
 .D END^RGED2
 E  D LEFT^RGED2(),POS^RGED2,DELETE(1)
 I $G(RGITER)>1 S RGITER=RGITER-1 G RBT
 Q
 ; Set flag for exit
EXIT S RGFIN=1
 Q
 ; Quit: prompt if changes have been made
QUIT D:RGBUF'="RGED" SWITCH^RGED5("")
 I RGCHG S RGZ=$$PRMPT^RGED2(-50,1,"U","YN^") Q:U[RGZ  D:RGZ="Y" SAVE
 S RGFIN=0
 Q
 ; Single character input
READCH() N RGZ
 S @$$TRAP^RGUTOS("READCHX^RGED0")
 R *RGZ:DTIME
 I  Q $C($S(RGZ:RGZ,1:255))
READCHX S DTOUT=1
 Q ""
 ; Error trap
ERROR Q:$G(DTOUT)
 S @$$TRAP^RGUTOS("ERROR^RGED0"),RGTBL=1,RGITER=0,RGITER2=0
 W $$PRMPT^RGED2(-44,0)
 K RGFIN
 G LOOP
