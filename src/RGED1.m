RGED1 ;MSC/IND/DKM - Continuation of RGED;04-May-2006 08:19;DKM
 ;;3.0;EXTENSIBLE EDITOR;;Jan 23, 2015;Build 12
 ;;
 ;=================================================================
 ; Save current state for user
SAVEST I '$G(DUZ) W $$PRMPT^RGED2(-4,0) Q
 K ^RGED("S",RGCF,DUZ)
 F RGZ=0:0 S RGZ=+$O(RGST(RGZ)) Q:'RGZ  S:$G(RGST(RGZ,-4)) ^RGED("S",RGCF,DUZ,RGZ)=RGST(RGZ)
 S ^RGED("R",RGCF,DUZ)=$TR(RGRULER,"qvwnv","==TT")
 W $$PRMPT^RGED2(5,0)
 Q
 ; Restore saved state
RESTST I '$G(DUZ) W $$PRMPT^RGED2(-4,0) Q
 S:$D(^RGED("R",RGCF,DUZ)) RGRULER=$TR(^(DUZ),"=T","qw")
 F RGZ=0:0 S RGZ=+$O(^RGED("S",RGCF,DUZ,RGZ)) Q:'RGZ  D:$G(RGST(RGZ,-4)) SHST^RGEDS(RGZ,^(RGZ))
 D PAINT^RGEDS
 Q
 ; Break a line at the current position
BREAK I $D(^XTMP(RGPID,RGBUF,RGLC))  ; Set naked reference
 F RGZ=RGLC:-1:RGROW+1 S ^(RGZ+1)=^(RGZ)
 W $S(RGROW>RGLC:$C(13),1:"")_RGEOL
 S RGPAD=$S(RGROW>RGLC:0,RGCOL=1:0,RGST(7)>0:RGST(7)-1,1:0),RGLC=RGLC+1,RGZ=$E(RGLN,RGCOL,RGMAX),RGLN=$E(RGLN,1,RGCOL-1),$X=0
 I ($L(RGZ)+RGPAD)'>RGMAX S RGZ=$$REPEAT^XLFSTR(" ",RGPAD)_RGZ
 E  W $$PRMPT^RGED2(-3,0) S RGPAD=0
 S ^XTMP(RGPID,RGBUF,RGROW+1)=RGZ,RGCHG=1
 I RGY<RGY2 D
 .W $$XY^RGUT(RGED1,RGY+1),RGESC_"[L"
 .D SLINE^RGEDS(RGROW+1)
 Q
 ; Concatenate two lines
JOIN I (RGROW>RGLC)!(RGROW=RGLC&$L(RGLN)) W $$PRMPT^RGED2(-15,0) Q
 N RGZ,RGZ1
 S RGZ=$G(^XTMP(RGPID,RGBUF,RGROW+1)),RGCHG=1
 F RGZ1=1:1 Q:$E(RGZ)'=" "  S RGZ=$E(RGZ,2,999)
 S:$C(9)_" @^*+-/=_\|~"'[$E(RGLN,$L(RGLN)) RGZ=" "_RGZ
 I ($L(RGLN)+$L(RGZ))>RGMAX W $$PRMPT^RGED2(-3,0) Q
 S ^XTMP(RGPID,RGBUF,RGROW+1)=RGZ
JOIN1 I RGROW>RGLC W $$PRMPT^RGED2(-15,0) Q
 I $D(^XTMP(RGPID,RGBUF,RGLC))  ; Set naked reference
 S:RGROW<RGLC RGLN=RGLN_^(RGROW+1),^(RGROW)=RGLN
 F RGZ=RGROW+1:1:RGLC-1 S ^(RGZ)=^(RGZ+1)
 K ^(RGLC)
 S RGLC=RGLC-1,RGCHG=1
 W $$XY^RGUT(RGED,RGY)
 D SLINE^RGEDS(RGROW)
 I RGY<RGY2 D
 .W !,RGESC_"[M",$$XY^RGUT(0,RGY2)
 .D SLINE^RGEDS(RGTOP+RGY2-RGY1)
 Q
 ; Perform line wrap
WRAP N RGZ,RGZ1,RGZ2,RGRSAV,RGCSAV,RGBRK
 S (RGZ,RGBRK)=RGST(8),RGZ2="@%^&*)_-+=]}\|;:<,>.?/~`"
 Q:RGZ'<$L(RGLN)
 F RGZ=RGZ:-1:RGZ-30 S RGZ1=$E(RGLN,RGZ) Q:RGZ1=""  I $A(RGZ1)<34!(RGZ2[RGZ1) S RGBRK=RGZ+1 Q
 F RGZ=RGBRK:1:$L(RGLN) S RGZ1=$E(RGLN,RGZ) Q:$A(RGZ1)'<34&(RGZ2'[RGZ1)  S RGBRK=RGZ+1
 Q:RGBRK'<$L(RGLN)
 S RGRSAV=RGROW,RGCSAV=RGCOL
 S:RGCOL'<RGBRK RGRSAV=RGROW+1,RGCSAV=RGBRK-RGCOL-1
 S RGZ=$G(^XTMP(RGPID,RGBUF,RGROW+1)),RGZ1=$L(RGZ),RGZ2=$L($TR(RGZ,$C(9,32)))
 I 'RGZ2!((RGZ1+$L(RGLN)-RGBRK)>RGST(8))!(RGROW'<RGLC) D  Q
 .D MOVETO^RGED2(RGROW,RGBRK),POS^RGED2,BREAK,POS^RGED2,MOVETO^RGED2(RGRSAV,$S(RGCSAV>0:RGCSAV,1:RGPAD-RGCSAV))
 F RGZ2=1:1:RGZ1+1 Q:$A(RGZ,RGZ2)'=32
 S:RGZ2>RGZ1 RGZ2=RGST(7),RGZ1=RGZ2-1,RGZ=$$REPEAT^XLFSTR(" ",RGZ1)
 S RGPAD=RGZ2-1,^XTMP(RGPID,RGBUF,RGROW+1)=$E(RGZ,1,RGPAD)_$E(RGLN,RGBRK,999)_$S($E(RGLN,$L(RGLN))=" ":"",1:" ")_$E(RGZ,RGZ2,999),RGLN=$E(RGLN,1,RGBRK-1)
 D SLINE^RGEDS(RGROW)
 W !
 I RGY<RGY2 D
 .D SLINE^RGEDS(RGROW+1)
 E  W RGEOL
 D MOVETO^RGED2(RGRSAV,$S(RGCSAV>0:RGCSAV,1:RGPAD-RGCSAV))
 Q
 ; Format a paragraph
FORMAT(RGITER) ;
 S RGITER=+$G(RGITER)
 D:RGSR1 SELECT^RGED5
FM0 D UPDATE^RGED2
 F RGZ=RGROW:-1:0 Q:'RGZ  Q:$TR(^XTMP(RGPID,RGBUF,RGZ),$C(9,32))=""
 D MOVETO^RGED2(RGZ+1,1)
FM1 I ($TR(RGLN,$C(9,32))="")!(RGROW>RGLC) D  G FM0:RGITER>0,PAINT^RGEDS
 .D MOVETO^RGED2(RGROW+1,1)
 .S:RGROW>RGLC RGITER=0
 .S RGMSG=1,RGITER=RGITER-1
 D WRAP
 F RGZ=1:1 Q:$A(RGLN,RGZ)'=32
 D:RGZ'=RGST(7) JUSTIFY^RGED4(-1)
FM2 S RGMSG=0
 I $L(RGLN)<RGST(8),RGROW<RGLC,$TR($G(^XTMP(RGPID,RGBUF,RGROW+1)),$C(9,32))'="" D JOIN,POS^RGED2 G:'RGMSG FM2 S RGMSG=0
 D WRAP,DOWN^RGED2()
 G FM1
 ; Set/Toggle tab stop
TGLTAB(RGC,RGSET,RGF) ;
 N RGZ
 S:'$G(RGC) RGC=RGCOL
 S RGZ=$E(RGRULER,RGC),$E(RGRULER,RGC)=$S('$D(RGSET):$TR(RGZ,"qwvn","wqnv"),RGSET:$TR(RGZ,"qv","wn"),1:$TR(RGZ,"wn","qv"))
 D:'$G(RGF) RULER^RGEDS
 Q
 ; Clear all tab stops
CLRTAB D SETTAB($L(RGRULER)+1)
 Q
 ; Set tabs every X stops
SETTAB(X) ;
 S:'$G(X) X=+$$GETNUM(68,5)
 Q:+X<1
 N RGZ
 S X=+X,RGRULER=$TR(RGRULER,"wn","qv")
 F RGZ=X:X:$L(RGRULER) D TGLTAB(RGZ,1,1)
 D RULER^RGEDS
 Q
 ; Set # screen rows
SETROWS(X) ;
 S:'$G(X) X=$$GETNUM(71,3,RGST(12))
 Q:X<5!(X>999)
 S RGST(12)=X
 D PAINT^RGEDS
 Q
 ; Get a numeric entry using specified prompt and length
GETNUM(RGPRMPT,RGLEN,RGDATA) ;
 N RGZ
 S RGZ=$TR($$PRMPT^RGED2(RGPRMPT,.RGLEN,"","0123456789^",.RGDATA)," ")
 Q $S(RGZ[U:U,1:+RGZ)
 ; Goto specified line
GOTO(RGGT) ;
 S:$G(RGGT)="" RGGT=$$PRMPT^RGED2(11,10,"","0123456789^+-")
 I $E(RGGT)="+" S RGGT=RGROW+$E(RGGT,2,99)
 E  I $E(RGGT)="-" S RGGT=RGROW-$E(RGGT,2,99)
 E  S RGGT=+RGGT Q:'RGGT
 S RGGT=$S(RGGT<1:1,RGGT>RGLC:RGLC+1,1:RGGT)
 D MOVETO^RGED2(RGGT,RGCOL)
 Q
 ; Set left margin
LM(RGC) S:'$G(RGC) RGC=RGCOL
 I RGST(8),RGC>(RGST(8)-5) W $$PRMPT^RGED2(-12,0) Q
 D RMLM(7,RGC),RULER^RGEDS
 Q
 ; Set right margin
RM(RGC) S:'$G(RGC) RGC=RGCOL
 I RGST(7),RGC<(RGST(7)+5) W $$PRMPT^RGED2(-13,0) Q
 D RMLM(8,RGC),RULER^RGEDS
 Q
RMLM(X,Y) ;
 S Y=$G(Y,RGST(X))
 S $E(RGRULER,RGST(X))=$TR($E(RGRULER,RGST(X)),"vn","qw")
 S $E(RGRULER,Y)=$TR($E(RGRULER,Y),"qw","vn")
 S RGST(X)=Y
 Q
 ; Set iteration count
ITER(RGCNT) ;
 S RGITER2=+$S('$D(RGCNT):$$GETNUM(56,7,$S(RGITER:RGITER,1:"")),1:RGCNT),RGITER=0
 Q
 ; Set bookmark
BMSET(RGR,RGC) ;
 S RGBMR=$S($G(RGR):RGR,1:RGROW),RGBMC=$S($G(RGC):RGC,1:RGCOL)
 W $$PRMPT^RGED2(65,0)
 Q
 ; Goto bookmark
BMFND I $G(RGBMR) D MOVETO^RGED2(RGBMR,RGBMC) Q
 W $$PRMPT^RGED2(-66,0)
 Q
