RGEDH ;MSC/IND/DKM - Continuation of RGED;04-May-2006 08:19;DKM
 ;;3.0;EXTENSIBLE EDITOR;;Jan 23, 2015;Build 12
 ;;
 ;=================================================================
 ; General help
GEN N RGZ
 S RGZ=$P(^RGEDCFG(RGCF,0),U,2)
 D HELP(RGZ,RGZ,"HELP")
 Q
 ; Move to next keyword
TAB D FIND()
 Q
 ; Move to previous keyword
BKTB D FIND(,1)
 Q
 ; Find a keyword starting with RGCH1
FIND(RGCH1,RGD) ;
 N RGZ
 S RGD=''$G(RGD),RGCH1=$C(127)_$G(RGCH1)
 I RGD F RGZ=RGCOL:-1:1 I $E(RGLN,RGZ)=$C(127) D MOVETO^RGED2(RGROW,RGZ) Q
 D SHST^RGEDS(3,RGD),SRCRPL^RGEDF(RGCH1)
 I RGMSG D
 .N RGSROW,RGSCOL
 .S RGSROW=RGROW,RGSCOL=RGCOL,RGMSG=0
 .D TOP^RGED2:'RGD,BTM^RGED2:RGD,SRCRPL^RGEDF(RGCH1)
 .I RGMSG D MOVETO^RGED2(RGSROW,RGSCOL) Q
 .S RGMSG=-1
 .D HLPMSG^RGEDS
 D RIGHT^RGED2(1):'RGMSG,FLUSH
 Q
 ; Flush type-ahead buffer
FLUSH N RGZ
 S RGCH=""
 F RGZ=0:0 R *RGZ:0 E  Q
 Q
 ; Traverse a hypertext link
LINK N RGZ,RGZ1,RGZ2
 F RGZ=RGCOL:-1:1 Q:$E(RGLN,RGZ)=$C(127)
 I $E(RGLN,RGZ)'=$C(127) W RGBEL Q
 F RGZ=RGZ+1:1 Q:$A(RGLN,RGZ)'=32
 F RGZ1=RGZ:1 Q:$C(128)[$E(RGLN,RGZ1)  S:$A(RGLN,RGZ1)'=32 RGZ2=RGZ1
 D KEYW($E(RGLN,RGZ,$S($D(RGZ2):RGZ2,1:RGZ1-1)))
 Q
 ; Load hypertext indexed by RGKEY
KEYW(RGKEY) ;
 N RGZ
 S RGKEY=$E(RGKEY,1,30),RGZ=$$UP^XLFSTR(RGKEY)
 S:RGZ'="" RGZ=$O(@RGHLP(0)@(RGLINK(RGLINK),2,"B",$E(RGZ,1,30),0))
 S:RGZ RGZ=+$P(@RGHLP(0)@(RGLINK(RGLINK),2,RGZ,0),U,2)
 I '$D(@RGHLP(0)@(+RGZ,0)) W $$PRMPT^RGED2(-33,0) Q
 S RGLINK(RGLINK,0)=RGED_U_RGY_U_RGLFT_U_RGTOP_U_RGROW_U_RGCOL_U_RGTTL_U_RGOBJ
 S RGTTL=$P(^(0),U,2),RGOBJ=RGKEY_" ["_RGLINK_"]"
 S RGLINK=RGLINK+1,RGLINK(RGLINK)=RGZ,RGED=RGED1,RGY=RGY1,(RGLFT,RGTOP,RGROW,RGCOL)=1
 ; Load a new help frame
LOAD S RGDIC=$NA(@RGHLP(0)@(RGLINK(RGLINK),1)),RGMSG=1
 D LOAD^RGED0,PAINT^RGEDS
 Q
 ; Goto entry level help frame
HOME Q:RGLINK=1
 S RGLINK=2
 ; Back up one help frame
BACK I RGLINK=1 W RGBEL Q
 S RGLINK=RGLINK-1,RGZ=RGLINK(RGLINK,0)
 S RGED=$P(RGZ,U),RGY=$P(RGZ,U,2),RGLFT=$P(RGZ,U,3),RGTOP=$P(RGZ,U,4),RGROW=$P(RGZ,U,5),RGCOL=$P(RGZ,U,6),RGTTL=$P(RGZ,U,7),RGOBJ=$P(RGZ,U,8)
 G LOAD
 ; Context-sensitive help
CSH N RGCSH,RGKEY,RGZ
 S RGCSH=$P(^RGEDCFG(RGCF,0),U,3)
 S:RGCSH'="" RGCSH=+$O(@RGHLP(0)@("B",RGCSH,0))
 I 'RGCSH W $$PRMPT^RGED2(-61,0) Q
 S RGKEY=$$PARSE,RGZ=$$FNDKEY(RGCSH,RGKEY)
 I 'RGZ W:$L(RGKEY) $$PRMPT^RGED2(-33,0) Q
 D HELP(RGZ,RGCSH,RGKEY)
 Q
 ; Return IEN of frame indexed by RGKEY in RGHF
FNDKEY(RGHF,RGKEY) ;
 Q:'$L(RGKEY) 0
 N RGZ,RGZ1
 S RGZ=+$O(@RGHLP(0)@(RGHF,2,"B",$E(RGKEY,1,30),0)),RGZ1="?"
 I 'RGZ F  S RGZ1=$O(@RGHLP(0)@(RGHF,2,"B",RGZ1)) Q:$E(RGZ1)'="?"  I RGKEY?@$E(RGZ1,2,99) S RGZ=+$O(^(RGZ1,0)) Q
 Q +$P($G(@RGHLP(0)@(RGHF,2,RGZ,0)),U,2)
 ; Load and begin a help session
HELP(RGHFM,RGHTL,RGHKEY) ;
 N RGLINK
 S:RGHFM'=+RGHFM&(RGHFM'="") RGHFM=+$O(@RGHLP(0)@("B",RGHFM,0))
 I 'RGHFM W:$D(RGCF) $$PRMPT^RGED2(-32,0) Q
 S RGHTL=$G(RGHTL)
 S:RGHTL=+RGHTL RGHTL=$P($G(@RGHLP(0)@(RGHTL,0)),U,2)
 S:'$L(RGHTL) RGHTL=$P($G(@RGHLP(0)@(RGHFM,0)),U,2)
 S RGLINK=1,RGLINK(1)=RGHFM
 W:$D(RGCF) $$PRMPT^RGED2(18,0)
 D ENTRY^RGED($NA(@RGHLP(0)@(RGHFM,1)),RGHTL,$G(RGHKEY),"HELP"),PAINT^RGEDS:$D(RGCF)
 I $D(RGCF) X ^%ZOSF("EOFF"),^("TRMON") Q
 X ^%ZOSF("EON"),^("TRMOFF")
 Q
 ; Parse out a keyword from current edit position
PARSE() N RGZ,RGZ1,RGZ2,RGZ3
 S RGZ3=$TR(RGLN,"_","X")
 F RGZ=$S(RGCOL>$L(RGZ3):$L(RGZ3),1:RGCOL):1 Q:$E(RGZ3,RGZ)'=" "
 I $E(RGZ3,RGZ)'?1P F RGZ=RGZ:-1:1 S RGZ2=$E(RGZ3,RGZ) I " "[RGZ2!(RGZ2?1P) S RGZ=RGZ+1 Q
 F RGZ1=RGZ:1 S RGZ2=$E(RGZ3,RGZ1) Q:RGZ2?.1P  I RGZ2=" " S RGZ1=RGZ1-1 Q
 I RGZ2?1P,RGZ1'=RGZ S RGZ1=RGZ1-1
 Q $$UP^XLFSTR($E(RGLN,RGZ,RGZ1))
