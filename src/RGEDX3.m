RGEDX3 ;MSC/IND/DKM - Spell checking extension ;04-May-2006 08:19;DKM
 ;;3.0;EXTENSIBLE EDITOR;;Jan 23, 2015;Build 12
 ;;
 ;=================================================================
SPELL N RGR,RGR1,RGC1,RGC2,RGC3,RGC4,RGWC,RGOPT,RGRSAV,RGCSAV,X,X0,X1,X2,RGZ
 I '$O(^RGEDDCT(0)) W $$PRMPT^RGED2("Spell checker not installed",0) Q
 K ^TMP("RGEDSKP",$J,RGPID)
 D MSG,SELECT^RGED5:RGSR1
 S RGOPT="",X2="",RGWC=0,RGRSAV=RGROW,RGCSAV=RGCOL
 F RGR=1:1:RGLC D  Q:RGOPT=U
 .D MOVETO^RGED2(RGR,1)
 .F RGC1=1:1:$L(RGLN) D:$E(RGLN,RGC1)'?.1P.1C  Q:RGOPT=U
 ..F RGC2=RGC1+1:1:$L(RGLN)+1 Q:$E(RGLN,RGC2)?.1P.1C!(RGOPT=U)
 ..S X1=$E(RGLN,RGC1,RGC2-1),RGC3=RGC1,RGC1=RGC2
 ..I X2="",$E($TR($E(RGLN,RGC2,999)," "))="-" S X2=X1,RGC4=RGC3,RGR1=RGR Q
 ..I $$LKP(X2_X1) S X2="" Q
 ..I X2'="",$$LKP(X2) S X2="" Q:$$LKP(X1)
 ..D:X2'="" BACK
 ..D NOT
 I RGOPT'=U,X2'="" D
 .D BACK
 .S RGOPT="X"
X2 .I "^I"'[RGOPT,'$$LKP(X1) D NOT S RGC2=RGC3+$L(X1) G X2
 W $$PRMPT^RGED2("Word count = |RGWC|",0)
 K ^TMP("RGEDSKP",$J,RGPID)
 D MOVETO^RGED2(RGRSAV,RGCSAV)
 Q
BACK S RGC3=RGC4,(RGC1,RGC2)=RGC3+$L(X2),RGR=RGR1,X1=X2,X2=""
 D MOVETO^RGED2(RGR,1)
 Q
NOT D MOVETO^RGED2(RGR,RGC3),SELECT^RGED5,MOVETO^RGED2(RGR,RGC2)
 S RGOPT=$$PRMPT^RGED2(RGBEL_"Not found:  Add, Edit, Ignore, ignore Rest, Suggest, or ^ to abort: ",1,"U","AEIRS^")
 S:RGOPT="R" ^TMP("RGEDSKP",$J,RGPID,$$UP^XLFSTR(X1))=""
 S:"RI^"[RGOPT RGWC=RGWC+1
 D ADD:RGOPT="A",EDIT:RGOPT="E",SUGGEST:RGOPT="S",SELECT^RGED5
MSG W $$PRMPT^RGED2("Spell checking in progress...",0)
 Q
EDIT S X1=$$PRMPT^RGED2("Edit word:  ",999,"","",X1)
 D REPL(X1)
 Q
REPL(X1) I X1'=U D
 .S RGLN=$E(RGLN,1,RGC3-1)_X1_$E(RGLN,RGC2,RGMAX),X=$$UP^XLFSTR(RGLN),RGCHG=1,RGC1=RGC3-1
 .W $$XY^RGUT(RGED1,RGR-RGTOP+RGY1)
 .D SLINE^RGEDS(RGR)
 Q
ADD I 'DUZ S RGZ=$$PRMPT^RGED2(RGBEL_"You must be identified to select this option, press a key to continue: ",1),RGZ="N"
 E  S RGZ=$$UP^XLFSTR($$PRMPT^RGED2("Add to your private dictionary? ",1))
 I "YN"'[RGZ!'$L(RGZ) W RGBEL G ADD
 I RGZ="Y" S ^RGED("D",RGCF,DUZ,$$UP^XLFSTR(X1))="",RGWC=RGWC+1
 E  S RGC1=RGC3-1
 Q
SUGGEST N RGSX,RGZX,RGZD,X,Y
 S RGSX=$$SOUNDEX^RGUT(X1),X=""
 I '$D(^RGEDDCT("AC",RGSX)) D  Q
 .S X1=$$PRMPT^RGED2("No suggestions.  Edit word:  ",999,"","",X1)
 .D REPL(X1)
 S Y=$$LKP^RGED4("^RGEDDCT","UX2","Replace """_X1_""" with: ","AC",RGSX,"","",0,RGY1)
 I Y>0 D
 .S X=$$LOW^XLFSTR($P(^RGEDDCT(Y,0),U))
 .S RGZX=$A(X1),RGZD=0
 .I RGZX>64,RGZX<91 S $E(X)=$$UP^XLFSTR($E(X))
 .F RGZX=1:1:$L(X1) I $A(X1,RGZX)>96,$A(X1,RGZX)<123 S RGZD=1 Q
 .S:'RGZD X=$$UP^XLFSTR(X)
 .S X1=X
 D PAINT^RGEDS,REPL(X1):Y>0
 Q
LKP(X) Q:X="" 1
 S X=$$UP^XLFSTR(X),X=$S(X?.N:1,X?.N1"1ST":1,X?.N1"2ND":1,X?.N1"3RD":1,X?1.N1"TH":1,$D(^RGEDDCT("B",X)):1,$D(^RGED("D",RGCF,DUZ,X)):1,$D(^TMP("RGEDSKP",$J,RGPID,X)):1,1:0),RGWC=RGWC+X
 Q X
