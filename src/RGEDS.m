RGEDS ;MSC/IND/DKM - Continuation of RGED;04-May-2006 08:19;DKM
 ;;3.0;EXTENSIBLE EDITOR;;Jan 23, 2015;Build 12
 ;;
 ;=================================================================
 ; Paint screen
PAINT D SCRN(RGST(11),RGST(12)),STATUS,SHOW
 Q
 ; Display screen
SHOW W $$XY^RGUT(0,RGY1-1),RGNORM
 F RGLNM=RGTOP:1:RGTOP+(RGY2-RGY1) W ! D SLINE(RGLNM)
 W $$XY^RGUT(RGED,RGY),$$RV(RGROW,RGCOL)
 Q
 ; Show line # RGLNM
SLINE(RGLNM) ;
 N RGLNX,RGZ,RGZ1
 W *13,RGNORM,RGEOL,*13
 I RGLNM>RGLC W:RGLC+1=RGLNM RGGRON,*13,$$REPEAT^XLFSTR("q",RGED2-RGED1+1),RGGROF,*13 Q
 E  I 'RGST(13) S RGLNX=$TR($E($S(RGLNM=RGROW:RGLN,1:$G(^XTMP(RGPID,RGBUF,RGLNM))),RGLFT,RGLFT+RGED2),RGCTL1,RGCTL2)
 E  S RGLNX=$G(^XTMP(RGPID,RGBUF,RGLNM))
 G S1:RGST(13)=1,S2:RGLNM=RGSR1,S3:RGLNM=RGSR2
 W $$RV(RGLNM,1),*13,RGLNX
S0 W *13,RGNORM
 Q
S1 S RGLNX=$E(RGLNX,RGLFT,RGLFT+RGED2)
 F  S RGZ=$F(RGLNX,$C(127)) Q:'RGZ  D
 .S RGZ1=$F(RGLNX,$C(128),RGZ)
 .S:'RGZ1 RGZ1=9999
 .W $E(RGLNX,1,RGZ-2),RGRVON," ",$E(RGLNX,RGZ,RGZ1-2)," ",RGNORM
 .S RGLNX=$E(RGLNX,RGZ1,RGMAX),$X=0
 W RGLNX
 Q
S2 G:RGSR1=RGSR2 S4
 W $$RV(RGLNM,1),*13,$E(RGLNX,1,RGSC1-RGLFT),$$RV(RGLNM,RGSC1),$E(RGLNX,RGSC1-RGLFT+1,999)
 G S0
S3 W $$RV(RGLNM,1),*13,$E(RGLNX,1,RGSC2-RGLFT),$$RV(RGLNM,RGSC2),$E(RGLNX,RGSC2-RGLFT+1,999)
 G S0
S4 I RGSC1<RGSC2 S RGZ=RGSC1,RGZ1=RGSC2
 E  S RGZ=RGSC2,RGZ1=RGSC1
 W $$RV(RGLNM,1),*13,$E(RGLNX,1,RGZ-RGLFT),$$RV(RGLNM,RGZ),$E(RGLNX,RGZ-RGLFT+1,RGZ1-RGLFT),$$RV(RGLNM,RGZ1),$E(RGLNX,RGZ1-RGLFT+1,999)
 G S0
 ; Initialize display
SCRN(X,Y) ;
 S X=$G(X,$S(RGST(11)>80:80,1:132)),Y=$G(Y,RGST(12))
 S RGED2=X-1,RGY2=Y-2
 S:RGED2<(RGED1+10) RGED2=79,X=80
 S:RGY2<(RGY1+5) RGY2=RGY1+5,Y=RGY2+2
 D SHST(11,X),SHST(12,Y)
 F X=$S(X>80:"3h",1:"3l"),"7l","4l","5l","6l","7l" D
 .W *13,RGESC_"[?"_X
 W *13,RGNORM_RGGROF,RGCLR_RGESC_"["_(RGY1+1)_";"_(RGY2+1)_"r",!
 S RGMSG=1
 D RM^RGUTOS(0),MOVETO^RGED2(RGROW,RGCOL)
 Q
 ; Set up the title and status lines
STATUS N RGZ
 W $$XY^RGUT(0,0),RGUNON,*13,RGTTL,?(RGED2-$L(RGOBJ)+1),RGOBJ,!,RGNORM,RGHION,RGGRON,!,$$REPEAT^XLFSTR("q",RGED2-RGED1+1),!,RGGROF,RGNORM
 F RGZ=0:0 D SHST(RGZ) S RGZ=+$O(RGST(RGZ)) Q:'RGZ
 Q
 ; Display and optionally modify status of specified state variable.
 ; If RGTGL not specified, state is unchanged.
 ; If RGTGL<0 state is advanced by one.
 ; If RGTGL>=0, state is changed to that value.
SHST(RGSTX,RGTGL) ;
 I 'RGSTX W $$XY^RGUT(RGED1,RGY1-1),RGHION,RGGRON,$S(RGLFT=1:"q",1:"`"),RGGROF,RGNORM Q
 N RGZ
 S:$D(RGTGL) RGST(RGSTX)=$S(RGTGL<0:RGST(RGSTX)+1,1:+RGTGL)
 S RGTGL=RGST(RGSTX)
 S:RGTGL>RGST(RGSTX,-1) (RGTGL,RGST(RGSTX))=0
 X:$D(RGST(RGSTX,-3)) RGST(RGSTX,-3)
 Q:'$D(RGST(RGSTX,-2))
 S RGZ=RGST(RGSTX,RGTGL)
 S:$L(RGZ) RGZ="u"_RGZ_"t"
 S RGZ=$$REPEAT^XLFSTR("q",RGST-$L(RGZ)\2)_RGZ
 W RGHION,RGGRON,$$XY^RGUT(RGST(RGSTX,-2)*RGST+1,RGY1-1),RGZ_$$REPEAT^XLFSTR("q",RGST-$L(RGZ)),RGGROF,RGNORM
 Q
 ; Display the ruler
RULER N RGZ,RGZ1
 W $$XY^RGUT(RGED1,RGY2+1),RGNORM,RGEOL,*13
 Q:RGST(6)=2
 S RGZ=RGLFT+RGED2,RGRULX=-1
 W RGHION,RGGRON,*13,$E(RGRULER,RGLFT,RGZ),*13,RGGROF,RGNORM
 Q
 ; Show current position on ruler
SPSN W:RGRULX'<0 *13,RGHION,RGGRON,$$XY^RGUT(RGRULX,RGY2+1),$E(RGRULER,RGRULX+RGLFT),*13,RGGROF,RGNORM
 S RGRULX=RGED
 W *13,RGRVON,RGGRON,$$XY^RGUT(RGED,RGY2+1),$E(RGRULER,RGCOL),*13,RGGROF,RGNORM
 Q
 ; Update message line
HLPMSG S:RGMSG RGMSG=+$$PRMPT^RGED2(RGHLP,0,1)
 D POSMSG
 Q
 ; Update position tracking info
POSMSG Q:'RGST(5)
 N RGZ
 S RGZ=$A(RGLN,RGCOL),RGZ=$S(RGZ<0:"",1:"    Char: "_RGZ)
 W RGRVON,$$XY^RGUT(RGED2\2+2,1),"Line: "_RGROW_"    Col: "_RGCOL_RGZ_RGEOL_RGNORM,$$XY^RGUT(RGED,RGY)
 Q
 ; Select video attribute for given row and column
RV(RGR,RGC) ;
 Q:RGR>RGLC ""
 N RGZ,RGZX,RGZY,RGR1,RGR2,RGC1,RGC2
 S RGZX=$X,RGZY=$Y
 I RGSR2'<RGSR1 S RGR1=RGSR1,RGC1=RGSC1,RGR2=RGSR2,RGC2=RGSC2
 E  S RGR1=RGSR2,RGC1=RGSC2,RGR2=RGSR1,RGC2=RGSC1
 I RGR1=RGR2,RGC1>RGC2 S RGZ=RGC1,RGC1=RGC2,RGC2=RGZ
 I 'RGR1!(RGR<RGR1)!(RGR>RGR2) W RGNORM G RV1
 I RGR>RGR1,RGR<RGR2 W RGRVON G RV1
 I RGR=RGR1,RGC<RGC1 W RGNORM G RV1
 I RGR=RGR2,RGC'<RGC2 W RGNORM G RV1
 W RGRVON
RV1 S $X=RGZX,$Y=RGZY
 Q ""
