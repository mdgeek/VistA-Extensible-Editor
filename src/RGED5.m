RGED5 ;MSC/IND/DKM - Continuation of RGED;04-May-2006 08:19;DKM
 ;;3.0;EXTENSIBLE EDITOR;;Jan 23, 2015;Build 12
 ;;
 ;=================================================================
 ; Drop/remove select anchor
SELECT D SHST^RGEDS(14,-1)
 I 'RGSR1 S (RGSR1,RGSR2)=RGROW,(RGSC1,RGSC2)=RGCOL
 E  D
 .N RGZ,RGR1,RGR2
 .S RGR1=RGSR1,RGR2=RGROW,(RGSR1,RGSR2)=0
 .S:RGR2<RGR1 RGZ=RGR1,RGR1=RGR2,RGR2=RGZ
 .S:RGR1<RGTOP RGR1=RGTOP
 .S RGZ=RGY2-RGY1+RGTOP
 .S:RGR2>RGZ RGR2=RGZ
 .W $$XY^RGUT(RGED1,RGR1-RGTOP+RGY1-1)
 .F RGZ=RGR1:1:RGR2 W ! D SLINE^RGEDS(RGZ)
 .D POS^RGED2
 Q
 ; Cut selected text to another buffer
CUT(RGNEW) ;
 D COPY(.RGNEW,1)
 Q
 ; Copy selected text to another buffer
COPY(RGNEW,RGCUT) ;
 I 'RGSR1 W $$PRMPT^RGED2(-29,0) Q
 S RGNEW=$$BGETN(.RGNEW,63)
 Q:RGNEW=""
 I RGNEW="RGED" W $$PRMPT^RGED2(-30,0) Q
 I RGNEW=RGBUF W $$PRMPT^RGED2(-25,0) Q
 D SHST^RGEDS(14,-1),UPDATE^RGED2
 K ^XTMP(RGPID,RGNEW)
 S:RGSR2<RGSR1 RGZ=RGSR2,RGSR2=RGSR1,RGSR1=RGZ,RGZ=RGSC2,RGSC2=RGSC1,RGSC1=RGZ
 I RGSR1=RGSR2 S:RGSC2<RGSC1 RGZ=RGSC2,RGSC2=RGSC1,RGSC1=RGZ
 S RGZ1=0
 F RGZ=RGSR1:1:RGSR2 D
 .S RGZ1=RGZ1+1,^XTMP(RGPID,RGNEW,RGZ1)=$E($G(^XTMP(RGPID,RGBUF,RGZ)),$S(RGZ=RGSR1:RGSC1,1:1),$S(RGZ=RGSR2:RGSC2-1,1:999))
 I $G(RGCUT) D
 .S RGZ2=RGSR2-RGSR1,RGCHG=1
 .S ^(RGSR1)=$E($G(^XTMP(RGPID,RGBUF,RGSR1)),1,RGSC1-1)_$E($G(^(RGSR2)),RGSC2,999)
 .I RGZ2 D
 ..I $D(^XTMP(RGPID,RGBUF,RGLC))  ; Set naked reference
 ..F RGZ=RGSR1+1:1:RGLC-RGZ2 S ^(RGZ)=^(RGZ+RGZ2)
 ..F RGZ=RGLC-RGZ2+1:1:RGLC K ^(RGZ)
 ..S RGLC=RGLC-RGZ2,RGLN=$G(^(RGROW))
 .D MOVETO^RGED2(RGSR1,RGSC1)
 S ^XTMP(RGPID,RGNEW,0)=U_U_RGZ1_U_RGZ1,(RGSR1,RGSR2,RGSC1,RGSC2)=0,RGLN=$G(^XTMP(RGPID,RGBUF,RGROW))
 D SHOW^RGEDS
 Q
 ; Switch to another buffer
SWITCH(RGNEW) ;
 S RGNEW=$$BGETN(.RGNEW,20)
 I RGNEW'="",RGNEW'="RGED@" D BSAVE(RGBUF),BLOAD(RGNEW)
 Q
 ; Get and validate buffer name
BGETN(RGB,RGP) ;
 N RGZ
 S:'$D(RGB) RGB=$$PRMPT^RGED2(RGP,1)
 S RGB=$$UP^XLFSTR(RGB)
 I RGB?.1AN!(RGB="@") S RGZ="RGED"_RGB
 E  W:RGB'=U $$PRMPT^RGED2(-21,0) Q ""
 I RGB?1N,'$D(^XTMP(RGPID,RGZ)) M ^XTMP(RGPID,RGZ)=^RGED("B",DUZ,RGB)
 Q RGZ
 ; Save buffer parameters
BSAVE(RGBUF) ;
 S ^XTMP(RGPID,RGBUF)=RGED_U_RGY_U_RGROW_U_RGCOL_U_RGLFT_U_RGTOP_U_RGLC_U_RGSR1_U_RGSC1_U_RGSR2_U_RGSC2_U_RGCHG_U_RGBMR_U_RGBMC,^(RGBUF,0)=U_U_RGLC_U_RGLC
 Q
 ; Load buffer parameters
BLOAD(RGNEW) ;
 N RGZ
 D UPDATE^RGED2
 S RGZ=$G(^XTMP(RGPID,RGNEW))
 I RGZ="" S RGED=RGED1,RGY=RGY1,(RGROW,RGCOL,RGLFT,RGTOP)=1,(RGSR1,RGSC1,RGSR2,RGSC2,RGCHG,RGBMR,RGBMC)=0,RGLC=+$P($G(^XTMP(RGPID,RGNEW,0)),U,3)
 E  D
 .S RGED=+RGZ,RGY=$P(RGZ,U,2),RGROW=$P(RGZ,U,3),RGCOL=$P(RGZ,U,4),RGLFT=$P(RGZ,U,5)
 .S RGTOP=$P(RGZ,U,6),RGLC=$P(RGZ,U,7),RGSR1=$P(RGZ,U,8),RGSC1=$P(RGZ,U,9)
 .S RGSR2=$P(RGZ,U,10),RGSC2=$P(RGZ,U,11),RGCHG=$P(RGZ,U,12),RGBMR=$P(RGZ,U,13),RGBMC=$P(RGZ,U,14)
 S RGBUF=RGNEW,RGLN=$G(^XTMP(RGPID,RGBUF,RGROW)),RGNEW=$A(RGBUF,5)
 D SHST^RGEDS(1,$S(RGNEW=-1:0,RGNEW<58:RGNEW-20,1:RGNEW-63)),SHST^RGEDS(14,''RGSR1),SHST^RGEDS(0),SHOW^RGEDS
 Q
 ; Delete a buffer
BDEL(RGNEW) ;
 S RGNEW=$$BGETN(.RGNEW,22)
 Q:RGNEW=""
 I RGNEW="RGED" W $$PRMPT^RGED2(-23,0) Q
 D:RGNEW=RGBUF BLOAD("RGED")
 K ^XTMP(RGPID,RGNEW)
 S ^(RGNEW)=""
 W $$PRMPT^RGED2(24,0)
 Q
