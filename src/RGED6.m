RGED6 ;MSC/IND/DKM - Continuation of RGED;04-May-2006 08:19;DKM
 ;;3.0;EXTENSIBLE EDITOR;;Jan 23, 2015;Build 12
 ;;
 ;=================================================================
 ; Paste text into current buffer
PASTE(RGNEW,RGITER) ;
 S RGNEW=$$BGETN^RGED5(.RGNEW,64)
 Q:U[RGNEW
 I $G(RGNEW)=RGBUF W $$PRMPT^RGED2(-25,0) Q
 I '$O(^XTMP(RGPID,RGNEW,0)) W $$PRMPT^RGED2(-26,0) Q
 D IMPORT(RGNEW,"B",.RGITER)
 Q
 ; Import text from a FileMan file, ASCII file, routine, buffer, or global reference
IMPORT(RGIMP,RGT,RGITER) ;
 N RGMSG2,RGZ,RGZ1,RGZ2
 S:$G(RGT)="" RGT=$$PRMPT^RGED2(42,1)
 S RGT=$F("BFGRTK",$$UP^XLFSTR(RGT))-1,RGIMP=$G(RGIMP),RGITER=+$G(RGITER),RGMSG2=0
 I RGT<1 W $$PRMPT^RGED2(-43,0) Q
 I "34"[RGT,$G(DUZ(0))'["@" W $$PRMPT^RGED2(-67,0) Q
 I RGT'=6,RGIMP="" S RGIMP=$$PRMPT^RGED2(41)
 I RGT'=6,U[RGIMP W $$PRMPT^RGED2(0,0) Q
 W $$PRMPT^RGED2(6,0)
 D @("IMP"_RGT),PAINT^RGEDS
 W $$PRMPT^RGED2(RGMSG2,0)
 Q
 ; Import (paste) a buffer
IMP1 S:$L(RGIMP)<2 RGIMP=$$BGETN^RGED5(RGIMP)
 I RGIMP=RGBUF S RGMSG2=-25
 E  S:RGIMP'="" RGMSG2=$$IMPGBL("^XTMP(RGPID,"""_RGIMP_""")",RGITER,1)
 Q
 ; Import another entry in source file
IMP2 S RGZ2=RGIMP,RGIMP=RGDIC
 S RGZ=$P(RGIMP,"("),RGIMP=$P(RGIMP,"(",2,999)
 I RGIMP="" S RGMSG2=-60 Q
 S RGIMP=$$CREF^DILF(RGZ_"("_$P(RGIMP,",",1,$L(RGIMP,",")-2))
 I '$D(@RGIMP@("B")),$E($O(^("B")))'="B" S RGMSG2=-60 Q
 K RGZ
 I $G(DUZ(0))'["@" D  I RGZ="" S RGMSG2=-67 Q
 .S RGZ=$O(^RGEDCFG(RGCF,7,"B",$TR(RGIMP,U,"~"),0))
 .S:RGZ (RGZ,RGZ(1))=$P($G(^RGEDCFG(RGCF,7,RGZ,1)),"=",2,999)
 W $$XY^RGUT(0,RGY1),RGEOS,!
 S RGZ1=$$LKP^RGED4(RGIMP,"XIFP*^","Entry to import: ","",RGZ2,"RGZ","",0,RGY1)
 S:RGZ1>0 RGIMP=$P(RGDIC,"(",2,999),$P(RGIMP,",",$L(RGIMP,",")-1)=RGZ1,RGMSG2=$$IMPGBL($P(RGDIC,"(")_"("_RGIMP,RGITER)
 Q
 ; Import a global
IMP3 S RGMSG2=$$IMPGBL(RGIMP,RGITER)
 Q
 ; Import a MUMPS routine
IMP4 I '$$TEST^RGUTOS(RGIMP) S RGMSG2=-28 Q
 K ^XTMP(RGPID,"RGED6")
 F RGZ=1:1 D  Q:RGZ1=""
 .S RGZ1=$T(+RGZ^@RGIMP)
 .S:$L(RGZ1) RGZ2=$P(RGZ1," "),RGZ1=RGZ2_$E("        ",$L(RGZ2)+1,99)_" "_$P(RGZ1," ",2,RGMAX)
 .S ^XTMP(RGPID,"RGED6",RGZ+1,0)=RGZ1
 S ^XTMP(RGPID,"RGED6",1,0)="*"_RGIMP
 D IMPTMP(RGZ+1)
 Q
 ; Import a sequential file
IMP5 S @$$TRAP^RGUTOS("IMP5ERR^RGED6")
 I RGIMP["*" D  Q:Y<1
 .W $$XY^RGUT(RGED1,RGY1),RGEOS,*13
 .D DIR^RGUTOS(RGIMP,100,"^TMP(""DIR"",$J,""B"")")
 .S RGZ1=""
 .F RGZ=1:1 S RGZ1=$O(^TMP("DIR",$J,"B",RGZ1)) Q:RGZ1=""  S ^(RGZ1,RGZ)="",^TMP("DIR",$J,RGZ,0)=RGZ1
 .S Y=$$LKP^RGED4("^TMP(""DIR"",$J)","X2","","B","*")
 .S:Y>0 RGIMP=^TMP("DIR",$J,Y,0)
 .K ^TMP("DIR",$J)
 D OPEN^RGUTOS(.RGIMP,"R")
 U RGIMP
 K ^XTMP(RGPID,"RGED6")
 F RGZ=1:1 Q:$$READ^RGUTOS(.RGZ1)  S ^XTMP(RGPID,"RGED6",RGZ,0)=RGZ1
 D CLOSE^RGUTOS(.RGIMP)
 U IO
 D IMPTMP(RGZ-1)
 Q
IMP5ERR S RGMSG2=-28
 Q
 ; Kermit import
IMP6 N XTKDIC,XTKMODE,XTKERR,DWLC
 K ^XTMP(RGPID,"RGED6")
 S XTKDIC="^XTMP(RGPID,""RGED6"",",XTKMODE=2,DWLC=0
 W $$XY^RGUT(0,RGY1),RGEOS,!!
 D RECEIVE^XTKERMIT
 S RGMSG2=$P(XTKERR," ",2,99)
 D:'XTKERR IMPTMP(DWLC)
 Q
 ; Import global text into buffer
IMPGBL(RGIMP,RGITER,RGT) ;
 N RGLCNT,RGMSG2,RGZ,RGZ1,RGZ2
 S RGIMP=$$CREF^DILF(RGIMP),RGT=+$G(RGT),@$$TRAP^RGUTOS("IMPERR^RGED6")
 S RGLCNT=+$P(@RGIMP@(0),U,4)
 Q:'RGLCNT 52
 I 'RGT,$$PRMPT^RGED2(27,1,"U","YN^","Y")'="Y" Q 69
 S RGCHG=1,RGMSG2=52
 D UPDATE^RGED2
 S:RGROW>RGLC RGLC=RGLC+1,^XTMP(RGPID,RGBUF,RGLC)="",RGLN=""
 F RGITER=+$G(RGITER):-1 D  Q:RGITER<2!(RGMSG2<0)
 .S RGLC=RGLC+RGLCNT-1
 .I RGLCNT>1 D
 ..I $D(^XTMP(RGPID,RGBUF,RGLC))  ; Set naked reference
 ..F RGZ=RGLC:-1:RGROW+RGLCNT-1 S ^(RGZ)=^(RGZ-RGLCNT+1)
 ..S ^(RGROW)=$E(RGLN,1,RGCOL-1),^(RGROW+RGLCNT-1)=$E(RGLN,RGCOL,RGMAX)
 .S RGZ=^XTMP(RGPID,RGBUF,RGROW),RGZ1=$$IMGET(1),RGZ2=$$REPEAT^XLFSTR(" ",RGCOL-$L(RGZ)-1)
 .S ^XTMP(RGPID,RGBUF,RGROW)=$E(RGZ,1,RGCOL-1)_RGZ2_RGZ1_$E(RGZ,RGCOL,RGMAX)
 .F RGZ=1:1:RGLCNT-2 S ^XTMP(RGPID,RGBUF,RGZ+RGROW)=$$IMGET(RGZ+1)
 .S:RGLCNT>1 RGZ=$$IMGET(RGLCNT),RGZ2=RGROW+RGLCNT-1,^(RGZ2)=RGZ_^XTMP(RGPID,RGBUF,RGZ2)
 .S RGLN=$G(^XTMP(RGPID,RGBUF,RGROW))
 Q RGMSG2
IMPERR Q -28
 ; Retrieve line from import global
IMGET(RGL) ;
 Q $S(RGT:$G(@RGIMP@(RGL)),1:$G(@RGIMP@(RGL,0)))
 ; Import from temporary global
IMPTMP(RGZ) ;
 S ^XTMP(RGPID,"RGED6",0)=U_U_RGZ_U_RGZ
 S RGMSG2=$$IMPGBL("^XTMP(RGPID,""RGED6""",RGITER)
 K ^XTMP(RGPID,"RGED6")
 Q
